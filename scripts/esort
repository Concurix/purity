#!/usr/bin/env escript
%% -*- erlang -*-
%% Sort file using the Erlang builtin sort function.

main([Input, Output]) ->
    writelines(Output, lists:sort(readlines(Input)));
main([]) ->
    io:format("~s", [lists:sort(getlines([]))]);
main(_) ->
    io:format("usage: ~s input output~n", [filename:basename(escript:script_name())]),
    halt(1).

writelines(Filename, Lines) ->
    file:write_file(Filename, Lines).

readlines(Filename) ->
    {ok, Io} = file:open(Filename, [read]),
    readlines(Io, []).

readlines(Io, Lines) ->
    case file:read_line(Io) of
        {ok, Line} ->
            readlines(Io, [Line|Lines]);
        eof ->
            lists:reverse(Lines)
    end.

getlines(Lines) ->
    case io:get_line("") of
        eof ->
            lists:reverse(Lines);
        Line ->
            getlines([Line|Lines])
    end.
